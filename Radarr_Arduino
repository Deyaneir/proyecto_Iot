#include <Servo.h>
#include <SoftwareSerial.h>

// PINES
const int pinPIR = 2;
const int led1 = 4;
const int led2 = 3;
const int trigPin = 6;
const int echoPin = 7;
const int pinIR = 9;
const int pinBuzzer = 8;
const int servoBarridoPin = 5;
const int servoIRPin = 10;

SoftwareSerial wifi(11, 12); 

Servo servoBarrido;
Servo servoIR;

int angulo = 0;
int paso = 2;

void setup() {
  pinMode(pinPIR, INPUT);
  pinMode(pinIR, INPUT);
  pinMode(led1, OUTPUT);
  pinMode(led2, OUTPUT);
  pinMode(pinBuzzer, OUTPUT);
  pinMode(trigPin, OUTPUT);
  pinMode(echoPin, INPUT);

  servoBarrido.attach(servoBarridoPin);
  servoIR.attach(servoIRPin);

  Serial.begin(9600); 
  wifi.begin(9600);  
  
  wifi.println("AT"); 
}

void loop() {
  int estadoPIR = digitalRead(pinPIR);
  digitalWrite(led1, estadoPIR);
  digitalWrite(led2, estadoPIR);

  int estadoIR = digitalRead(pinIR);
  if (estadoIR == LOW) {
    servoIR.write(90);
    tone(pinBuzzer, 1000);
  } else {
    servoIR.write(0);
    noTone(pinBuzzer);
  }

  moverRadar(estadoPIR, estadoIR);
  delay(20);
}

void moverRadar(int pir, int ir) {
  servoBarrido.write(angulo);

  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);

  long duracion = pulseIn(echoPin, HIGH, 30000);
  float distancia = (duracion > 0) ? (duracion * 0.0343 / 2.0) : 0;
  String data = String(angulo) + "," + String(distancia) + "," + String(pir) + "," + String(ir);

  // ENVIAR LA DATA A PC
  Serial.println(data);

  // ENVIAR A WIFI (
  wifi.println(data);

  angulo += paso;
  if (angulo >= 180 || angulo <= 0) {
    paso = -paso;
  }
}
